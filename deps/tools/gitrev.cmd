@echo off
IF NOT EXIST lastrev.txt (
    echo 0 > lastrev.txt
)

set REVISION=

IF EXIST "C:\Program Files\Git\bin\git.exe" (
    SET FOUNDGIT="C:\Program Files\Git\bin\git.exe"
) ELSE IF EXIST "C:\Program Files (x86)\Git\bin\git.exe" (
    SET FOUNDGIT="C:\Program Files (x86)\Git\bin\git.exe"
)

if defined FOUNDGIT (
    FOR /F %%G IN ('git rev-list HEAD --count') DO IF NOT DEFINED REVISION SET REVISION=%%G
) else (
    SET REVISION=99999
)


FOR /F %%H IN (lastrev.txt) DO SET LASTREVISION=%%H

echo Current revision is %REVISION%
echo Last revision is %LASTREVISION%

IF %LASTREVISION% NEQ %REVISION% (
    echo // generated by gitrev.cmd > ..\..\steam_api\buildnumber.h
    echo #define BUILDNUMBER %REVISION% >> ..\..\steam_api\buildnumber.h
    echo #define BUILDNUMBER_STR "%REVISION%" >> ..\..\steam_api\buildnumber.h
    echo #define BUILDHOST "%COMPUTERNAME%" >> ..\..\steam_api\buildnumber.h
    echo %REVISION% > lastrev.txt
)